# 寄存器

- 段寄存器	CS DS SS ES
- 通用寄存器	AX BX CX DX
- 指令指针寄存器	IP
- 标志寄存器	

----

# 其他

实模式下的地址线是 20 位,寻址空间多少?

20条线,即最大能表示2^20-1 = 1048575,寻址范围为0x00000~0xFFFFF(范围为1048576 = 1024*1024),

结合16位寄存器,可以实现1MB的寻址范围:

​	16 bits 的程序计数器首先从存储器中取得指令放入8 bits 的指令寄存器。

​	指令寄存器暂存放入的指令然后交给译码器取进行解析。

​	指令译码器将解析后的命令交给控制逻辑部件去执行操作。

​	我们知道存储器的内部结构是以字节形式组织的，所以cpu每次只能取得一条指令然后执行，接着重复上述操作，直至所有指令全部执行完毕。

​	这就是早期的串行结构设计。

---

- 启动bochs `bin/bochs -f bochsrc.disk`

- 编译mbr `nasm -o mbr.bin mbr.s`
- 写mbr入硬盘 `dd if=./mbr.bin of=./hd60M.img bs=512 count=1 conv=notrunc`
- 指定库目录 `nasm -I include/ -o mbr.bin mbr.s`
- 编译loader `nasm -I include/ -o loader.bin loader.s`
- 写loader入硬盘 `dd if=./loader.bin of=./hd60M.img bs=512 count=1 seek=2 conv=notrunc`

----

实验一分析

- 存放BIOS程序的ROM空间被映射到实模式下1MB内存的顶部，即地址0xF0000-0xFFFFF处，共64KB,其中顶部的**0xFFFF0~0xFFFFF**这16B空间是BIOS程序的入口地址

- 开机后CS:IP值，其值是F000:FFF0,指令地址就是**0xFFFF0**。BIOS程序开始运行
  1. 检测和初始化硬件；
  2. 建立中断向量表（即硬件I/O的操作）
  3. 校验启动盘中位于0盘0道1扇区的内容（检测到末尾的两个字节分别是`0x55`和`0xaa`时，就认为该扇区存放MBR）
- 此代码通过改变寄存器的值,对应的调用多次INT 10H中断函数,来达到打印相应字符串的结果

```assembly
;主引导程序 
;------------------------------------------------------------
SECTION MBR vstart=0x7c00	;0x7c00是mbr的入口地址         
   mov ax,cs	;ax=0xF000
   mov ds,ax	;ds=0xF000
   mov es,ax	;es=0xF000
   mov ss,ax	;ss=0xF000
   mov fs,ax	;fs=0xF000
   mov sp,0x7c00	;sp=0x7c00

; 清屏 利用0x06号功能，上卷全部行，则可清屏。
; -----------------------------------------------------------
;INT 0x10   功能号:0x06	   功能描述:上卷窗口
;------------------------------------------------------
;输入：
;AH 功能号= 0x06
;AL = 上卷的行数(如果为0,表示全部)
;BH = 上卷行属性
;(CL,CH) = 窗口左上角的(X,Y)位置
;(DL,DH) = 窗口右下角的(X,Y)位置
;无返回值：
   mov     ax, 0x600	;ax=0x0600	清屏范围,也就是宽度
   mov     bx, 0x700	;bx=0x0700
   mov     cx, 0        ;cx=0x0000	左上角: (0, 0)
   mov     dx, 0x184f	;dx=0x184f	右下角: (80,25),
			   ; VGA文本模式中,一行只能容纳80个字符,共25行。
			   ; 下标从0开始,所以0x18=24,0x4f=79
   int     0x10            ; int 0x10

;;;;;;;;;    下面这三行代码是获取光标位置    ;;;;;;;;;
;.get_cursor获取当前光标位置,在光标位置处打印字符.
   mov ah, 3	;ah=0x03 ax=0x0300	输入: 3号子功能是获取光标位置,需要存入ah寄存器
   mov bh, 0	;bh=0x00 bx=0x0000	bh寄存器存储的是待获取光标的页号

   int 0x10		; 输出: ch=光标开始行(0x00),cl=光标结束行(0x00)
			; dh=光标所在行号(0x18),dl=光标所在列号(0x4f)

;;;;;;;;;    获取光标位置结束    ;;;;;;;;;;;;;;;;

;;;;;;;;;     打印字符串    ;;;;;;;;;;;
   ;还是用10h中断,不过这次是调用13号子功能打印字符串
   mov ax, message	;ax=message	ax保存字符串地址
   mov bp, ax	;bp=message 保存字符串地址 es:bp 为串首地址, es此时同cs一致(0xF000)，
			; 开头时已经为es初始化

   ; 光标位置要用到dx寄存器中内容,cx中的光标位置可忽略
   mov cx, 5		;cx=0x0005	保存字符串长度 cx 为串长度,不包括结束符0的字符个数
   mov ax, 0x1301	; 子功能号13是显示字符及属性,要存入ah寄存器,
			; al设置写字符方式 al=01: 显示字符串,光标跟随移动
   mov bx, 0x2		;bx=0x0002	bh(0x00)存储要显示的页号,此处是第0页,
			; bl(0x02)中是字符属性, 属性黑底绿字(bl = 02h)
   int 0x10		; 执行BIOS 0x10 号中断
   
;;;;;;;;;      打字字符串结束	 ;;;;;;;;;;;;;;;

   jmp $		; 使程序悬停在此

   message db "1 MBR"
   times 510-($-$$) db 0 ; 填充剩余的510字节的空间为0
   db 0x55,0xaa   ;mbr结束标志,为了让bios辨认

```



















































